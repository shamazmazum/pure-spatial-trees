<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <title>
  BK-trees &ndash; pure-spatial-trees
</title>
    <link rel="stylesheet" href="static/style.css"/>
    
  <link rel="stylesheet" href="static/highlight.css"/>
  <script src="static/highlight.js"></script>
  <script src="static/load-mathjax.js" async></script>
  <style>
   /* Highlight the current top-level TOC item, and hide the TOC of all other items */

   .toc a[data-node="bk-trees"] {
       /*color: #AD3108;*/
   }

   .toc ol {
       display: none;
   }

   .toc li a[data-node="bk-trees"] {
       font-weight: bold;
   }

   .toc li a[data-node="bk-trees"] + ol {
       display: block;
   }

   .toc li a[data-node="bk-trees"] + ol li {
       margin-left: 10px;
   }
  </style>

  </head>
  <body>
    
  <h1 class="doc-title">pure-spatial-trees</h1>
  <article id="article" data-section="bk-trees">
    <aside>
      <ol class="toc"><li><a href="index.html" data-node="index">Overview</a></li><li><a href="bk-trees.html" data-node="bk-trees">BK-trees</a><ol><li><a href="bk-trees.html#api" data-node="api">API</a></li></ol></li></ol>
    </aside>
    <main class="codex-section">
      <header>
        <h2 class="section-title">BK-trees</h2>
      </header>
      <div class="content">
        <p>
   BK-trees are trees for metric spaces with integral metric. They allow the
   following queries (sorted from the fastest queries to the slowest):
   </p><ul><li>Set membership test: Does an object belong to the tree?</li><li>Searching for objects which are close enough to the specified
   object.</li><li>Searching for the nearest neighbor(s).</li></ul><p>   Difference from <a href="https://github.com/vy/bk-tree">bk-tree</a>:
   </p><ul><li>bk-tree is not tested.</li><li>bk-tree produces wrong results (check bk-tree.lisp, line 145).</li><li>bk-tree modifies its arguments.</li></ul><p>   Here is an example for five-dimensional space with integer coordinates and
   Manhattan distance:
</p><pre><code class="lisp">(defpackage bk-example
  (:use #:cl)
  (:local-nicknames (#:sera #:serapeum)
                    (#:alex #:alexandria)
                    (#:bk   #:pure-spatial-trees/bk))
  (:export #:distance
           #:make-tree
           #:random-vector
           #:random-list

           #:items-in-ball))
(in-package :bk-example)

(sera:-&gt; distance ((simple-array fixnum (*))
                   (simple-array fixnum (*)))
         (values alex:non-negative-fixnum &amp;optional))
(defun distance (v1 v2)
  &quot;Calculate Manhattan distance between two vectors.&quot;
  (declare (optimize (speed 3)))
  (let ((v (map '(vector fixnum)
                (lambda (x y)(abs (- x y)))
                v1 v2)))
    (declare (dynamic-extent v))
    (reduce #'+ v)))

(sera:-&gt; random-vector (alex:positive-fixnum
                        alex:positive-fixnum)
         (values (simple-array fixnum (*)) &amp;optional))
(defun random-vector (dim max)
  &quot;Generate a vector of length DIM with random integer elements in the range
from 0 to 1000.&quot;
  (make-array dim
              :element-type 'fixnum
              :initial-contents (loop repeat dim collect (random max))))

(sera:-&gt; random-list (alex:positive-fixnum
                      alex:positive-fixnum
                      alex:positive-fixnum)
         (values list &amp;optional))
(defun random-list (n dim max)
  &quot;Generate a list of N random vectors.&quot;
  (remove-duplicates
   (loop repeat n collect (random-vector dim max))
   :test #'equalp))

(sera:-&gt; items-in-ball (list t alex:non-negative-fixnum)
         (values list &amp;optional))
(defun items-in-ball (list obj dist)
  &quot;Naïve O(n) implementation of search for vectors no farther than DIST from OBJ.&quot;
  (declare (optimize (speed 3)))
  (loop for elem in list
        for d = (distance obj elem)
        when (&lt;= d dist) collect elem))

(sera:-&gt; make-tree (list)
         (values bk:tree &amp;optional))
(defun make-tree (list)
  &quot;Make BK tree from a set of vectors with Manhattan distance between them.&quot;
  (reduce (lambda (tree x)
            (bk:insert tree x #'distance))
          list :initial-value nil))
</code></pre><p>Now try it in the REPL:
</p><pre><code class="lisp">CL-USER&gt; (defparameter *list* (bk-example:random-list 100000 5 1000))
*LIST*
CL-USER&gt; (length *list*)
100000 ; No duplicates
CL-USER&gt; (time (defparameter *tree* (bk-example:make-tree *list*)))
Evaluation took:
  25.026 seconds of real time
  25.203811 seconds of total run time (25.203609 user, 0.000202 system)
  [ Real times consist of 0.104 seconds GC time, and 24.922 seconds non-GC time. ]
  [ Run times consist of 0.283 seconds GC time, and 24.921 seconds non-GC time. ]
  100.71% CPU
  95,097,318,836 processor cycles
  9,688,798,720 bytes consed
  
*TREE*
CL-USER&gt; ;; Naïve appoach
(time
 (loop repeat 1000 do
       (bk-example:items-in-ball *list* (bk-example:random-vector 5 1000) 30)))
Evaluation took:
  24.226 seconds of real time
  24.336622 seconds of total run time (24.336622 user, 0.000000 system)
  [ Real times consist of 0.059 seconds GC time, and 24.167 seconds non-GC time. ]
  [ Run times consist of 0.171 seconds GC time, and 24.166 seconds non-GC time. ]
  100.46% CPU
  92,059,254,784 processor cycles
  6,400,403,840 bytes consed
  
NIL
CL-USER&gt; ;; Search with BK-tree
(time
 (loop repeat 1000 do
       (pure-spatial-trees/bk:items-in-ball
        *tree* (bk-example:random-vector 5 1000) 30 #'bk-example:distance)))
Evaluation took:
  1.883 seconds of real time
  1.883651 seconds of total run time (1.883649 user, 0.000002 system)
  100.05% CPU
  7,157,871,786 processor cycles
  397,533,952 bytes consed
  
NIL
CL-USER&gt; ;; Search with BK-tree (smaller radius)
(time
 (loop repeat 1000 do
       (pure-spatial-trees/bk:items-in-ball
        *tree* (bk-example:random-vector 5 1000) 10 #'bk-example:distance)))
Evaluation took:
  1.056 seconds of real time
  1.056159 seconds of total run time (1.056156 user, 0.000003 system)
  100.00% CPU
  4,013,380,032 processor cycles
  246,428,800 bytes consed
  
NIL
CL-USER&gt; ;; Membership test
(time
 (loop repeat 1000 do
       (pure-spatial-trees/bk:belongs-to-set-p
        *tree* (bk-example:random-vector 5 1000) #'bk-example:distance)))
Evaluation took:
  0.721 seconds of real time
  0.721491 seconds of total run time (0.721491 user, 0.000000 system)
  100.00% CPU
  2,741,624,874 processor cycles
  170,752,896 bytes consed
  
NIL
CL-USER&gt; 
</code></pre><p>   </p><h1 id="api">API</h1>
     All functions are found in <code>pure-spatial-trees/bk</code> package.
     
       <div class="codex-error codex-no-node">No node with name <code>insert</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>delete</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>nearest-neighbors</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>nearest-neighbor</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>items-in-ball</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>belongs-to-set-p</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>flatten</code>.</div>
       <div class="codex-error codex-no-node">No node with name <code>trees-equal-p</code>.</div>
     
   <p>
</p>
      </div>
    </main>
  </article>
  <footer>
    <div class="info">
      Created with <a href="https://github.com/CommonDoc/codex">Codex</a>.
    </div>
  </footer>
  <script>
   HighlightLisp.highlight_auto();
  </script>

  </body>
</html>
